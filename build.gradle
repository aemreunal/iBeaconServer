buildscript {
    def tomcatGradlePluginVersion = '1.2.4'

    // Repositories for the dependencies used for building the gradle script itself
    repositories {
        mavenCentral()
        jcenter()
    }

    // Dependencies used for building the gradle script itself
    dependencies {
        classpath "org.gradle.api.plugins:gradle-tomcat-plugin:${tomcatGradlePluginVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'tomcat'

// External libs
def tomcatVersion = '7.0.11'
def hibernateORMVersion = '4.3.6.Final'
def hibernateEntityManagerVersion = '4.3.6.Final'
def hibernateValidatorVersion = '5.1.1.Final'
def springFrameworkVersion = '4.0.6.RELEASE'
def springSecurityVersion = '3.2.4.RELEASE'

// Root Project
version = '0.1'

// Wrapper
def wrapperGradleVersion = '2.0'

// Repositories for the dependencies used for building the project
repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://repository.jboss.org/nexus/content/groups/public/org/hibernate'
    }
}

// Dependencies used for building the project
dependencies {
    // Shortcut: compile 'org.hibernate:hibernate-core:3.6.7.Final'
    compile group: 'org.hibernate', name: 'hibernate-core', version: hibernateORMVersion
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: hibernateEntityManagerVersion
    compile group: 'org.hibernate', name: 'hibernate-validator', version: hibernateValidatorVersion

    // Tomcat, via https://github.com/bmuschko/gradle-tomcat-plugin
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    tomcat "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }

    // Spring, via http://projects.spring.io/
    compile group: 'org.springframework', name: 'spring-context', version: springFrameworkVersion
    compile group: 'org.springframework.security', name: 'spring-security-web', version: springSecurityVersion

    // Tests
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// Tomcat configuration, via https://github.com/bmuschko/gradle-tomcat-plugin
tomcat {
    httpPort = 8090
    httpsPort = 8091
    enableSSL = true
}

task wrapper(type: Wrapper) {
    gradleVersion = wrapperGradleVersion
}

task listJars << {
    configurations.compile.each { File file -> println file.name }
}

println "Project is located in: " + projectDir
